apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.firebase.crashlytics'

// keystorePropertiesFile 변수를 만들고 프로젝트 폴더의 keystore.properties 파일로 초기화 한다.
def keystorePropertiesFile = rootProject.file("../jks/hotkey_keystore.properties")
// keystoreProperties 변수를 만들고 Properties() 객체를 초기화 한다.
def keystoreProperties = new Properties()
// keystore.properties 파일을 keystoreProperties 객체에 로드한다.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))


android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "kr.blogspot.ovsoce.hotkey"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 117
        int versionMajor = 0
        int versionMinor = 0
        int versionPatch = versionCode

        if (1000 > versionPatch && versionPatch > 99) {
            versionMajor = versionPatch / 100
            versionMinor = (versionPatch % 100) / 10
            versionPatch = (versionPatch % 100) % 10
        } else if (100 > versionCode && versionPatch > 9) {
            versionMajor = 0
            versionMinor = versionPatch / 10
            versionPatch = versionPatch % 10
        } else if (versionPatch < 10) {
            versionMajor = 0
            versionMinor = 0
            versionPatch = versionPatch / 1
        }

        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        multiDexEnabled true
    }

    signingConfigs {
        release {
            keyAlias keystoreProperties['keyAlias']
            storePassword keystoreProperties['storePassword']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue("string", "ads_app_id", "ca-app-pub-3940256099942544~3347511713")
            resValue("string", "ads_unit_id_banner", "ca-app-pub-3940256099942544/6300978111")
            resValue("string", "ads_unit_id_interstitial", "ca-app-pub-3940256099942544/1033173712")
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            resValue("string", "ads_app_id", "ca-app-pub-8679189423397017~9361379422")
            resValue("string", "ads_unit_id_banner", "ca-app-pub-8679189423397017/8758744192")
            resValue("string", "ads_unit_id_interstitial", "ca-app-pub-8679189423397017/2384988367")
            signingConfig signingConfigs.release
        }
    }

    /*configurations.all { exclude group: 'com.android.support', module: 'customtabs' }*/

    lintOptions {
        checkReleaseBuilds false
        // Or, if you prefer, you can continue to check for errors in release builds,
        // but continue the build even when errors are found:
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.13.1'
    implementation "androidx.appcompat:appcompat:1.2.0"
    implementation "androidx.browser:browser:1.3.0"
    implementation "androidx.palette:palette-ktx:1.0.0"
    implementation "androidx.cardview:cardview:1.0.0"
    implementation "com.google.android.material:material:1.2.1"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"

    implementation platform('com.google.firebase:firebase-bom:25.12.0')
    implementation "com.google.firebase:firebase-ads"
    implementation "com.google.firebase:firebase-crashlytics-ktx"
    implementation "com.google.firebase:firebase-analytics-ktx"
    implementation 'com.google.firebase:firebase-messaging-ktx'

    implementation "com.jakewharton:butterknife:10.2.1"
    annotationProcessor "com.jakewharton:butterknife-compiler:10.2.1"

    implementation "io.reactivex.rxjava2:rxjava:2.2.19"
    implementation "io.reactivex.rxjava2:rxandroid:2.1.1"
    implementation "com.jakewharton.rxbinding2:rxbinding:2.2.0"

    implementation "com.github.bumptech.glide:glide:$glide_version"
    annotationProcessor "com.github.bumptech.glide:compiler:$glide_version"

    implementation "com.yqritc:recyclerview-flexibledivider:$flexibledivider_version"
    implementation "com.facebook.stetho:stetho:$stetho_version"

    annotationProcessor "org.projectlombok:lombok:$lombok_version"
    compileOnly "javax.annotation:jsr250-api:$jsr250_version"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation "com.tbruyelle.rxpermissions2:rxpermissions:$rxpermissions"
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.4.20'
    implementation 'androidx.preference:preference-ktx:1.1.1'
}

apply plugin: 'com.google.gms.google-services'